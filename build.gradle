apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    //渠道信息，其中myserver，pgyer分别为自己服务器上传使用以及上传蒲公英平台使用
    productFlavors {
        xiaomi {}
        _360 {}
        baidu {}
        wandoujia {}
        umeng {}
        myserver {}
        pgyer {}
    }

    signingConfigs {

        //debug版本不签名
        debug {
        }

        //release版本签名信息
        release {
            storeFile file("gradle.jks")
            storePassword KEYSTORE_PASSWORD
            keyAlias "gradle"
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        //debug版本编译是配置
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        //release版本编译时配置
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为test_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "${APP_NAME}_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
}

//文件上传至蒲公英平台
task upload(type: Exec) {
    executable 'sh'
    args '-c','../upload.sh'
}